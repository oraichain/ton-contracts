;; ;; encode(message: SignerInfo, writer: BinaryWriter = BinaryWriter.create()): BinaryWriter {
;; ;;     if (message.publicKey !== undefined) {
;; ;;       Any.encode(message.publicKey, writer.uint32(10).fork()).ldelim();
;; ;;     }
;; ;;     if (message.modeInfo !== undefined) {
;; ;;       ModeInfo.encode(message.modeInfo, writer.uint32(18).fork()).ldelim();
;; ;;     }
;; ;;     if (message.sequence !== BigInt(0)) {
;; ;;       writer.uint32(24).uint64(message.sequence);
;; ;;     }
;; ;;     return writer;
;; ;;   },

;; ;; {
;; ;;     publicKey: {
;; ;;         typeUrl: '/cosmos.crypto.secp256k1.PubKey',
;; ;;         value: <Buffer 0a 21 02 a5 b6 ed 42 f3 c5 8a 8d dc c6 f5 fa ee 44 94 71 26 78 6a 55 8a 88 53 a4 65 c2 e2 d0 ee b0 4a 67>
;; ;;     },
;; ;;     modeInfo: { single: [Object], multi: undefined },
;; ;;     sequence: Long { low: 274922, high: 0, unsigned: true }
;; ;; }

#include "any_google.fc";
#include "varint.fc";
#include "mode_info.fc";

(int, tuple) signer_info::encode(slice ds) {
    slice any_data = ds~load_ref().begin_parse();
    slice mode_info = ds~load_ref().begin_parse();
    slice sequence = ds~load_ref().begin_parse();
    int length = 0;

    tuple return_tuple = empty_tuple();
    if (any_data.slice_refs_empty?() != -1) {
        builder inner_cell = begin_cell();
        (int encode_any_len, slice encode_type_url, tuple value_encoded) = any::encode(any_data);
        inner_cell = inner_cell
                        .store_slice(varuint::encode(10))
                        .store_slice(varuint::encode(encode_any_len))
                        .store_slice(encode_type_url);
        length += inner_cell.end_cell().begin_parse().slice_bits() >> 3;
        return_tuple~tpush(inner_cell.end_cell().begin_parse());
        int i = 0;
        while (i < value_encoded.tlen()) {
            slice data = value_encoded.at(i);
            length += data.slice_bits() >> 3;
            return_tuple~tpush(data);
            i += 1;
        }
    }

    if (mode_info.slice_empty?() != -1) {
        slice mode_info_encode = mode_info::encode(mode_info);
        slice mode_info_item = begin_cell()
                                .store_slice(varuint::encode(18))
                                .store_slice(varuint::encode(mode_info_encode.slice_bits() >> 3))
                                .store_slice(mode_info_encode)
                                .end_cell()
                                .begin_parse();
        return_tuple~tpush(mode_info_item);
        length += mode_info_item.slice_bits() >> 3;
    }

    if (sequence.slice_empty?() != -1) {
        slice sequence_item = begin_cell()
                                .store_slice(varuint::encode(24))
                                .store_slice(sequence)
                                .end_cell()
                                .begin_parse();
        return_tuple~tpush(sequence_item);
        length += sequence_item.slice_bits() >> 3;
    }

    return (length, return_tuple);
}
