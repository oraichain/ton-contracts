#include "imports/stdlib.fc";
#include "imports/op-codes.fc";
#include "utils/verify.fc";
#include "static/errors.fc";
#include "static/op_codes.fc";
#include "static/state.fc";
#include "utils/messages.fc";
#include "common/messages.fc";
#include "utils/jetton.fc";
#include "utils/helper.fc";


const msg_prefix = "7b227772697465223a7b2264617461223a"s;
const end_json = "7d7d"s;
const slice quotes = "22"s;

;; storage variables
global slice light_client; ;; TODO: dynamic calculate based on blockId
global slice bridge_wasm_smart_contract;
global cell jetton_wallet_code;

;; load_data populates storage variables using stored data
() load_data() impure {
    slice ds = get_data().begin_parse();
    light_client = ds~load_msg_addr();

    bridge_wasm_smart_contract = ds~load_ref().begin_parse();

    jetton_wallet_code = ds~load_ref();

    ds.end_parse();
}

;; save_data stores storage variables as a cell into persistent storage
() save_data() impure inline_ref {
    set_data(
        begin_cell()
        .store_slice(light_client)
        .store_slice(bridge_wasm_smart_contract)
        .store_ref(jetton_wallet_code)
        .end_cell()
    );
}



() confirm_tx (slice in_msg_body) impure {
    int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case
    (slice to, slice jetton_master, int amount, int src) = (in_msg_body~load_msg_addr(), in_msg_body~load_msg_addr(), in_msg_body~load_uint(128), in_msg_body~load_uint(32));
    if(src == src::cosmos){
        cell mint_msg = begin_cell()
                    .store_uint(op::internal_transfer(), 32)
                    .store_uint(query_id, 64)
                    .store_coins(amount)
                    .store_uint(0, 2)
                    .store_slice(my_address())
                    .store_coins(0)
                    .store_maybe_ref(null())
                    .end_cell();
      
        cell body = begin_cell()
                    .store_uint(op::mint(),32)
                    .store_uint(query_id,64)
                    .store_slice(to)
                    .store_coins(amount)
                    .store_ref(mint_msg)
                    .end_cell();
        
        send_simple_message(0, jetton_master, body, CARRY_REMAINING_GAS);
    }

    if(src == src::ton){
        int jetton_master_len = slice_bits(jetton_master);
        if(jetton_master_len == 2){
            send_empty_message(amount,to, SEND_MODE_PAY_FEES_SEPARETELY);
        } 

        if(jetton_master_len == 267) {
            slice bridge_jetton_wallet = calculate_user_jetton_wallet_address(my_address(), jetton_master, jetton_wallet_code);
            cell body = begin_cell()
                        .store_uint(op::transfer(), 32)
                        .store_uint(query_id, 64)
                        .store_coins(amount)
                        .store_slice(to)
                        .store_slice(my_address())
                        .store_maybe_ref(null())
                        .store_coins(0)
                        .store_maybe_ref(null())
                        .end_cell();
            send_simple_message(0, bridge_jetton_wallet, body, CARRY_REMAINING_GAS);
        }
    }
}


;; recv_internal is the main function of the contract and is called when it receives a message from other contracts
() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    if (in_msg_body.slice_empty?()) { ;; ignore all empty messages        
        return ();
    }
    slice cs = in_msg_full.begin_parse();
   
    int flags = cs~load_uint(4);

    if (flags & 1) { ;; ignore all bounced messages
        return ();
    }

    load_data(); ;; here we populate the storage variables
    
    slice sender_address = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32); ;; by convention, the first 32 bits of incoming message is the op
    if (op == op::send_tx) {
        int query_id = in_msg_body~load_uint(64); ;; also by convention, the next 64 bits contain the "query id", although this is not always the case
        int height = in_msg_body~load_uint(64);
        cell tx_raw_cell = in_msg_body~load_ref();
        cell proofs = in_msg_body~load_ref();
        cell positions = in_msg_body~load_ref();
        cell data = in_msg_body~load_ref();
        slice data_slice = data.begin_parse();
        slice tx_raw = tx_raw_cell.begin_parse();
        ;; basic validate
        throw_unless(error::empty_tx, tx_raw.slice_refs_empty?() == 0);
        slice auth_info = tx_raw~load_ref().begin_parse();
        slice tx_body = tx_raw~load_ref().begin_parse();
        slice signatures = tx_raw~load_ref().begin_parse();
        throw_unless(error::empty_auth_info, auth_info.slice_refs_empty?() == 0);
        throw_unless(error::empty_tx_body, tx_body.slice_refs_empty?() == 0);
        throw_unless(error::empty_tx_signatures, signatures.slice_refs_empty?() == 0);
        slice messages = tx_body~load_ref().begin_parse();

        while(messages.slice_refs_empty?() != -1) {
            slice next_ref = messages~load_ref().begin_parse();
            slice message = messages~load_ref().begin_parse();
            slice typeUrl = message~load_ref().begin_parse();
            slice value = message~load_ref().begin_parse();
            slice sender_contract = value~load_ref().begin_parse();
            sender_contract~load_ref(); ;; skip sender
            slice contract = sender_contract~load_ref().begin_parse();
            int is_execute_msg = typeUrl.slice_hash() == msg_execute_contract::typeUrl.slice_hash();
            int is_bridge_smart_contract = contract.slice_hash() == bridge_wasm_smart_contract.slice_hash();
            if(is_execute_msg & is_bridge_smart_contract){
                slice msg = value~load_ref().begin_parse();
                tuple tuple_msg = msg.slice_ref_to_tuple();
                tuple tuple_data = empty_tuple();
                tuple_data~tpush(msg_prefix);
                (slice f, slice b) = cs_hex_to_tuple_string(data_slice);
                tuple_data~tpush(quotes);
                tuple_data~tpush(f);
                tuple_data~tpush(b);
                tuple_data~tpush(quotes);
                tuple_data~tpush(end_json);
                int data_hash = digest_hash(tuple_data);
                int msg_hash = digest_hash(tuple_msg);
                if(data_hash = msg_hash){
                    ;; send_raw_message
                    builder bodyBuilder = begin_cell();
                    bodyBuilder = bodyBuilder 
                        .store_uint(op::verify_receipt, 32)
                        .store_uint(query_id, 64)
                        .store_ref(
                        begin_cell()
                            .store_uint(height, 64)
                            .store_ref(tx_raw_cell)
                            .store_ref(proofs)
                            .store_ref(positions)
                            .store_ref(data)
                            .end_cell()
                    );

                    cell body = bodyBuilder.end_cell();
                    send_simple_message(0, light_client, body, CARRY_REMAINING_GAS);
                }
            }
            messages = next_ref;
        }
        return ();
    } 
    
    if(op == op::confirm_tx){   
        throw_unless(error::invalid_sender, sender_address.slice_hash() == light_client.slice_hash()); ;; only light client can confirm tx
        confirm_tx(in_msg_body); 
        return ();
    }

    throw(0xffff); ;; if the message contains an op that is not known to this contract, we throw
}



(slice, slice ,cell) get_bridge_data() method_id {
    load_data();
    return (light_client, bridge_wasm_smart_contract, jetton_wallet_code);
}